buildscript {
    ext.kotlin_version = '1.2.30'

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'se.transmode.gradle:gradle-docker:1.2'
    }
}


def kodiak_docker_version = 'UNKNOWN'
if (hasProperty('docker_version')) {
    kodiak_docker_version = docker_version
}

def kodiak_docker_name = 'kodiak'
if (hasProperty('docker_name')) {
    kodiak_docker_name = docker_name
}

apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'docker'

mainClassName = "org.vaccineimpact.kodiak.MainKt"

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "com.google.code.gson:gson:2.2.4"
    compile "org.slf4j:slf4j-log4j12:1.7.6"
    compile "com.offbytwo:docopt:0.6.0.20150202"
    compile 'org.abstractj.kalium:kalium:0.7.0'
    compile('com.bettercloud:vault-java-driver:3.1.0')

    testCompile "junit:junit:4.12"
    testCompile "org.mockito:mockito-core:2.+"
    testCompile "com.nhaarman:mockito-kotlin:1.5.0"
    testCompile "org.assertj:assertj-core:3.9.1"
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

distDocker {
    setEnvironment("production", "true")
    baseImage = "docker.montagu.dide.ic.ac.uk:5000/openjdk-vault-libsodium:master"
    dependsOn = [build]
    push = true
    tag = kodiak_docker_name
    tagVersion = kodiak_docker_version
}

task copyDockerProperties(type: Copy) {
    from 'src/main/resources/docker'
    into 'src/main/resources'
    outputs.upToDateWhen { false }
}

task copyDevProperties(type: Copy) {
    from 'src/main/resources/dev'
    into 'src/main/resources'
    outputs.upToDateWhen { false }
}


task install_libsodium() {
    def file = file("/usr/local/lib/libsodium.so.23.1.0")
    if (!file.exists()) {
        throw new Exception("libsodium is not installed. Please run ../libsodium/install-libsodium.sh as root")
    }
}

if (System.getProperty("production")) {
    processResources.dependsOn copyDockerProperties
} else {
    processResources.dependsOn copyDevProperties
}

// usage: ./gradlew runAppLocally -PcommandLineArgs="init --github-token=token target1"
task runAppLocally(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath

    main = mainClassName
    if (project.hasProperty("commandLineArgs")) {
        args commandLineArgs.tokenize()
    }
}

build {
    dependsOn 'install_libsodium'
}
